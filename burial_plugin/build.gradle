plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly 'com.android.tools.build:gradle:8.5.2'
    compileOnly 'org.ow2.asm:asm:9.6'
    compileOnly 'org.ow2.asm:asm-commons:9.6'
}

group = 'com.github.zhiying94'
version = '2.0.5'

gradlePlugin {
    website = 'https://github.com/zhiying94/burialPlugin'
    vcsUrl = 'https://github.com/zhiying94/burialPlugin.git'
    plugins {
        burialPlugin {
            id = 'com.github.zhiying94.burial-plugin'
            displayName = 'Burial Plugin'
            description = 'A Gradle plugin for method timing and monitoring'
            implementationClass = 'com.yan.burial.BurialPlugin'
            tags = ['android', 'performance', 'monitoring']
        }
    }
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// 添加 JitPack 发布配置
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.getByName("java")
                groupId = 'com.github.zhiying94'
                artifactId = 'burial-plugin'
                version = project.version
                
                // 添加插件元数据
                pom {
                    name = 'Burial Plugin'
                    description = 'A Gradle plugin for method timing and monitoring'
                    url = 'https://github.com/zhiying94/burialPlugin'
                    
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}