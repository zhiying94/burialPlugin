plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly 'com.android.tools.build:gradle:8.5.2'
    compileOnly 'org.ow2.asm:asm:9.6'
    compileOnly 'org.ow2.asm:asm-commons:9.6'
}

group = 'com.zhiy.burial'
version = '2.0.0'

gradlePlugin {
    website = 'https://github.com/zhiying94/burialPlugin'
    vcsUrl = 'https://github.com/zhiying94/burialPlugin'
    plugins {
        burialPlugin {
            id = 'com.zhiy.burial'
            displayName = 'Burial Plugin'
            description = 'Android method timing instrumentation via ASM with AGP 8 instrumentation API'
            implementationClass = 'com.yan.burial.BurialPlugin'
            tags.set(['android','asm','instrumentation','bytecode','performance'])
        }
    }
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def userName
def userKey
File file = rootProject.file('local.properties')
if (file.exists()) {
    //加载资源
    InputStream inputStream = rootProject.file('local.properties').newDataInputStream();
    Properties properties = new Properties()
    properties.load(inputStream)

    if (properties.containsKey("bintray.user")) {
        userName = properties.getProperty("bintray.user")
    }
    if (properties.containsKey("bintray.apikey")) {
        userKey = properties.getProperty("bintray.apikey")
    }
}